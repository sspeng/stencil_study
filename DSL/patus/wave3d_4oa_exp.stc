stencil wave1D2oa (
    double grid A(0 .. x_max+2, 0 .. y_max+2, 0 .. z_max+2),
    float param fMin = -1,
    float param fDX = 2 / (x_max-3),
    float param fDT_DX_sq = 0.25)

{

	domainsize = (2 .. x_max, 2 .. y_max, 2 .. z_max);

  iterate while t < 100;

  //d2u/d2t = k * (d2u/d2x) 
  // Explicit method, All central differences

//  A[x;t-1] - 2 * A[x;t] + A[x;t+1] = c1 * (A[x-1;t] - 2 * A[x;t] + A[x+1;t]);
//    
//    A[x;t+1] = c1 * (A[x-1;t] - 2 * A[x;t] + A[x+1;t])
//       + 2 * A[x;t] - A[x;t-1];
//
  initial
  {

    A[x, y, z; -1] = sinf (2*π * ((x-1) * fDX + fMin)) * sinf (2*π * ((y-1) * fDX + fMin)) * sinf (2*π * ((z-1) * fDX + fMin));
    A[x, y, z; -1 : x==0 || x==x_max-1 || y==0 || y==y_max-1 || z==0 || z==z_max-1 ] = 0;
    A[x, y, z; 0] = A[x,y,z; -1];
    A[x, y, z; 1] = 0;
  }



	
	operation
	{

    c1 = 1.0/12.0;
    c2 = 4.0/3.0;
    c3 = 5.0/2.0;

    A[x,y,z;t+1] = 2 * A[x,y,z;t] - A[x,y,z;t-1] 
      + 0.125 * (-c1*A[x-2,y,z;t] + c2*A[x-1,y,z;t] - c3 * A[x,y,z;t] + c2*A[x+1,y,z;t] - c1*A[x+2,y,z;t])
      + 0.125 * (-c1*A[x,y-2,z;t] + c2*A[x,y-1,z;t] - c3 * A[x,y,z;t] + c2*A[x,y+1,z;t] - c1*A[x,y+2,z;t])
      + 0.125 * (-c1*A[x,y,z-2;t] + c2*A[x,y,z-1;t] - c3 * A[x,y,z;t] + c2*A[x,y,z+1;t] - c1*A[x,y,z+2;t])


//    A[x; t+1] = 
//      A[x;t] +
//      0.125 * (A[x-1;t] - 2 * A[x;t] + A[x+1;t]);
	}
}
